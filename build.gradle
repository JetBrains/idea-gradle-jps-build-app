import com.sun.management.OperatingSystemMXBean

import java.lang.management.ManagementFactory

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            url 'http://dl.bintray.com/jetbrains/intellij-plugin-service'
        }
        maven {
            url 'https://dl.bintray.com/jetbrains/intellij-third-party-dependencies/'
        }
    }
    dependencies {
        classpath "org.jetbrains.intellij.plugins:gradle-intellij-plugin:0.5.0-SNAPSHOT"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.5.30"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
}

apply plugin: 'org.jetbrains.intellij'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://www.jetbrains.com/intellij-repository/snapshots'
    }
}

dependencies {
    compile 'org.reflections:reflections:0.9.11'
}

intellij {
    def ideVersion = System.getenv("ide_version")
    if (ideVersion == null) {
        ideVersion = '211.7442.40'
    } else {
        println("Using IDE: $ideVersion")
    }
    version ideVersion

    def idePluginsList = System.getenv("ide_plugins")
    if (idePluginsList == null) {
        idePluginsList = 'java,gradle,org.jetbrains.kotlin:1.3.61-release-IJ2019.3-1'
    } else {
        println("Installing the following plugins: $idePluginsList")
    }

    plugins = idePluginsList.split(',')
    pluginName 'gradle-integration-testing-plugin'
    sandboxDirectory = "$project.rootDir/local"
}

def thread = new Thread(new Runnable() {
    @Override
    void run() {
        while (!Thread.currentThread().isInterrupted()) {
            def os = (OperatingSystemMXBean) ManagementFactory.getOperatingSystemMXBean();
            def memAll = Runtime.getRuntime().totalMemory();
            def memUsed  = Runtime.getRuntime().totalMemory() - Runtime.getRuntime().freeMemory();

            def swapAll = os.totalSwapSpaceSize.toInteger()

            def swapString = ""
            if(swapAll == 0) {
                swapString = "Swap = 0"
            } else {
                def swapUsed = swapAll - os.freeSwapSpaceSize
                swapString = "swap used = ${(swapUsed/swapAll*100).round(2)}%"
            }

            def systemCpuLoad = os.systemCpuLoad

            println("SystemCpuLoad = ${(systemCpuLoad*100).round(2)}%, mem used = ${(memUsed/memAll*100).round(2)}%, $swapString")
            Thread.sleep(30 * 1000)
        }
    }
})

tasks.getByName("runIde").doFirst {
    thread.setDaemon(true)
    thread.start()
}

tasks.getByName('runIde') {
    def workingDir = System.getenv("working_dir")
    if (workingDir == null) {
        workingDir = "../jps"
    }
    def testName = System.getenv("test_name")
    if (testName == null) {
        testName = 'JpsImportAndBuild'
    }
    def heapSize = System.getenv("idea_heap_size")
    if (heapSize == null) {
        heapSize = '4G'
    }
    def absoluteWorkingDir = new File(workingDir).getCanonicalFile().getAbsolutePath().toString()
    def containerDir = new File(absoluteWorkingDir).getParentFile().absolutePath
    println("Executing test [$testName] in folder [$absoluteWorkingDir]")
    systemProperties = ['java.awt.headless': 'true', 'projectPath': absoluteWorkingDir, 'jdkPath': System.getenv('JDK_18'), 'idea.project.path': containerDir]
    args = ['runIntegrationTest', testName]

    println("Setting heap size to $heapSize")
    maxHeapSize = heapSize

    if ("true" == System.getenv("enable_serial_gc")) {
        jvmArgs = ['-XX:+UseSerialGC', '-XX:MinHeapFreeRatio=1', '-XX:MaxHeapFreeRatio=99']
    }

    println("Using jvmArgs=$jvmArgs")


    jbrVersion = '11_0_8b944.34'
}
tasks.getByName("runIde").doLast {
    println("Thread interruped")
    thread.interrupt()
}

group 'org.jetbrains'
version '0.1'

